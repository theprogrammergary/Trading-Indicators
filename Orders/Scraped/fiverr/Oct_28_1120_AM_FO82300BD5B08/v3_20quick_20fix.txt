// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © GM_Trades

//@version=5
indicator("Fractal Liquidity Raid v3", overlay = true)

//FRACTAL CODE
show_fractals = input.bool(true, title = "Show Fractals", group = "Fractal Settings")
n = input.int(title='Fractal Period', defval=2, minval=2, group = "Fractal Settings")
fractalBars = input.string(title='3 or 5 Bar Fractal', defval='3', options=['3', '5'], group = "Fractal Settings")

// Logic 
dnFractal = if fractalBars == '5'
    dnFractal5 = high[n - 2] < high[n] and high[n - 1] < high[n] and high[n + 1] < high[n] and high[n + 2] < high[n]
    dnFractal5
else
    if fractalBars == '3'
        dnFractal3 = high[n - 1] < high[n] and high[n + 1] < high[n]
        dnFractal3

upFractal = if fractalBars == '5'
    upFractal5 = low[n - 2] > low[n] and low[n - 1] > low[n] and low[n + 1] > low[n] and low[n + 2] > low[n]
    upFractal5
else
    if fractalBars == '3'
        upFractal3 = low[n - 1] > low[n] and low[n + 1] > low[n]
        upFractal3
// END FRACTAL


//LIQUIDITY RAID
must_be_fractal = input.bool(false, title = "Raid Must Be Fractal", group = "Liquidity Raid Settings")
show_raid= input.string("Both",options = ["Both", "Up", "Down", "None"], title = "Show Raids", group = "Liquidity Raid Settings")
up_lookback = input.int(title='Up Fractal Lookback', defval=1, minval=1, group = "Liquidity Raid Settings")
down_lookback = input.int(title='Down Fractal Lookback', defval=1, minval=1, group = "Liquidity Raid Settings")
raid_offset = input.int(title='Icon Offset Value', defval=30, group = "Liquidity Raid Settings")

session_alerts = input.bool(false, title = "Session Alerts Only", group = "Liquidity Raid Settings")
seesion_time = input.session("0930-1600", title = "Session (EST Timezone)", group = "Liquidity Raid Settings")


//ARRAY
//UP FRACTALS
var float[] upFractals_body = array.new_float(up_lookback+1,0.00)
var float[] upFractals_wick = array.new_float(up_lookback+1,0.00)
var float raid_level_upfractals_body = 0.00
var float raid_level_upfractals_wick = 0.00

if upFractal
    array.unshift(upFractals_body, math.min(open[n],close[n]))
    array.pop(upFractals_body)
    
    array.unshift(upFractals_wick, low[n])
    array.pop(upFractals_wick)
    
up_must_be = must_be_fractal == false ? true : upFractal

//DEBUG CODE  
//is_upraid() =>
    //num_of_raids = 0
    //for i = 0 to array.size(upFractals_body) - 1 by 1
        //body_level = array.get(upFractals_body, i)
        //wick_level = array.get(upFractals_wick, i)
        //if upFractal and low[n] < wick_level and math.min(close[n],open[n]) >= body_level
            //num_of_raids := num_of_raids + 1
    //[num_of_raids > 0 , num_of_raids]
//[up_raid, num_of_raids]  = is_upraid()
//if num_of_raids != 0
    //label.new(bar_index - n, high, text = str.tostring(num_of_raids), style = label.style_none, textcolor = num_of_raids !=0 ? color.yellow : color.white)
    
//ACTUAL CODE
is_upraid() =>
    num_of_raids = 0
    for i = 0 to array.size(upFractals_body) - 1 by 1
        body_level = array.get(upFractals_body, i)
        wick_level = array.get(upFractals_wick, i)
        if up_must_be and low[n] < wick_level and math.min(close[n],open[n]) >= body_level
            num_of_raids := num_of_raids + 1
    num_of_raids > 0
up_raid  = is_upraid()
show_up_raids = show_raid == "Both" or show_raid == "Up"

//DOWN FRACTALS
var float[] downFractals_body = array.new_float(down_lookback+1,0.00)
var float[] downFractals_wick = array.new_float(down_lookback+1,0.00)
var float raid_level_downfractals_body = 0.00
var float raid_level_downfractals_wick = 0.00

if dnFractal
    array.unshift(downFractals_body, math.max(open[n],close[n]))
    array.pop(downFractals_body)
    
    array.unshift(downFractals_wick, high[n])
    array.pop(downFractals_wick)

down_must_be = must_be_fractal == false ? true : dnFractal
    
//ACTUAL CODE
is_downraid() =>
    num_of_raids = 0
    for i = 0 to array.size(downFractals_body) - 1 by 1
        body_level = array.get(downFractals_body, i)
        wick_level = array.get(downFractals_wick, i)
        if down_must_be and high[n] > wick_level and math.max(close[n],open[n]) <= body_level
            num_of_raids := num_of_raids + 1
    num_of_raids > 0
down_raid  = is_downraid()
show_down_raids = show_raid == "Both" or show_raid == "Down"

//ALERTS
isTime = time(timeframe.period, seesion_time,"America/New_York")
okForAlerts = session_alerts and isTime ? true : not session_alerts ? true : false
//barcolor(okForAlerts ? color.yellow : color.black)
alertcondition(okForAlerts and up_raid, title = "Up Fractal Raid", message = "Up Fractal Raid")
alertcondition(okForAlerts and down_raid, title = "Down Fractal Raid", message = "Down Fractal Raid")


//PLOTTING
plotshape(show_fractals ? upFractal: na, style=shape.triangleup, location=location.belowbar, offset=-n, color=color.new(color.green, 25), title = "Up Fractal")
//plot(raid_level_upfractals_body, title = "Up Fractal Raid Level Body", color = color.new(color.green,100), style = plot.style_circles)
//plot(raid_level_upfractals_wick, title = "Up Fractal Raid Level Wick", color = color.new(color.green,100))
plotchar(show_up_raids and up_raid ? low[n] - (syminfo.mintick * raid_offset) : na ,  offset=-n, title = "Up Raid", char = "✪", location = location.absolute, color= color.white, size = size.auto)


plotshape(show_fractals ? dnFractal : na, style=shape.triangledown, location=location.abovebar, offset=-n, color=color.new(color.red, 25), title = "Down Fractal")
//plot(raid_level_downfractals_body, title = "Down Fractal Raid Level Body", color = color.new(color.red,100), style = plot.style_circles)
//plot(raid_level_downfractals_wick, title = "Down Fractal Raid Level Wick", color = color.new(color.red,100))
plotchar(show_down_raids and down_raid ? high[n] + (syminfo.mintick * raid_offset) : na ,  offset=-n, title = "Down Raid", char = "✪", location = location.absolute, color= color.white, size = size.auto)

 

















